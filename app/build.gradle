apply plugin: 'com.android.application'
apply plugin: 'realm-android'
apply plugin: 'com.getkeepsafe.dexcount'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.google.firebase.firebase-crash'
apply plugin: 'com.google.firebase.firebase-perf'
apply plugin: 'io.fabric'

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()

android {
    compileSdkVersion 27
    buildToolsVersion "27.0.3"
    signingConfigs {
        if (keystorePropertiesFile.exists()) {
            keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
            release {
                keyAlias keystoreProperties['keyAlias']
                keyPassword keystoreProperties['keyPassword']
                storeFile file(keystoreProperties['storeFile'])
                storePassword keystoreProperties['storePassword']
            }
        }

    }
    defaultConfig {
        applicationId "org.sugarandrose.app"
        minSdkVersion 21
        targetSdkVersion 27
        versionCode 6
        versionName "1.0"
        vectorDrawables.useSupportLibrary = true

        multiDexEnabled true

        setProperty("archivesBaseName", "SugarAndRose-v$versionName-b$versionCode")

        buildConfigField "String", "BASE_URL", "\"https://sugarandrose.org/wp-json/wp/v2/\""
        buildConfigField "String", "WEB_PAGE", "\"https://sugarandrose.org/\""
        buildConfigField "String", "FB_NAME", "\"sugarandrose\""
        buildConfigField "String", "FB_ID", "\"1789118054658269\""
        buildConfigField "String", "INSTAGRAM_NAME", "\"sugarandrosen\""
        buildConfigField "String", "TWITTER_NAME", "\"sugarandrose\""
        buildConfigField "String", "TWITTER_ID", "\"199252121\""
        buildConfigField "String", "PINTEREST_NAME", "\"sugarandrosem\""
        buildConfigField "String", "EMAIL", "\"sugarandrosen@gmail.com\""
        buildConfigField "String", "IMPRESSUM", "\"https://sugarandrose.org/kontakt/impressum/\""

    }
    buildTypes {
        release {
            if (keystorePropertiesFile.exists()) {
                signingConfig signingConfigs.release
            }
            minifyEnabled true
            shrinkResources true
            proguardFiles 'proguard-rules.pro'
        }
    }
    flavorDimensions "backend"
    productFlavors {
        dev {
            signingConfig signingConfigs.debug
            applicationIdSuffix ".dev"
            resValue "string", "app_name", "sugar&rose DEV"
            dimension "backend"
        }
        prod {
            resValue "string", "app_name", "sugar&rose"
            dimension "backend"
        }
    }
    dataBinding {
        enabled = true
    }
    lintOptions {
        abortOnError false
    }
    testOptions {
        unitTests.returnDefaultValues = true
    }
    packagingOptions {
        // Remove unneeded Realm native libs (nearly no devices use these)
        exclude 'lib/mips/librealm-jni.so'
        exclude 'lib/x86_64/librealm-jni.so'
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }
}

dependencies {
    //local
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    //ui
    implementation "com.android.support:support-fragment:$supportLibVersion"
    implementation "com.android.support:support-core-ui:$supportLibVersion"
    implementation "com.android.support:appcompat-v7:$supportLibVersion"
    implementation "com.android.support:cardview-v7:$supportLibVersion"
    implementation "com.android.support:recyclerview-v7:$supportLibVersion"
    implementation "com.android.support:design:$supportLibVersion"
    implementation "com.android.support:customtabs:$supportLibVersion"
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
    kapt "com.android.databinding:compiler:$gradlePluginVersion"
    implementation 'jp.wasabeef:recyclerview-animators:2.2.7'
    implementation 'com.patloew.navigationviewfragmentadapters:adapters:0.3.0'
    implementation 'com.simplecityapps:recyclerview-fastscroll:1.0.17'

    //network
    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation "com.squareup.retrofit2:converter-gson:$retrofitVersion"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofitVersion"
    implementation "com.squareup.okhttp3:okhttp:$okHttpVersion"
    implementation "com.squareup.okhttp3:logging-interceptor:$okHttpVersion"
    implementation 'com.google.code.gson:gson:2.8.2'
    implementation 'org.jsoup:jsoup:1.11.2'

    //di
    kapt "com.google.dagger:dagger-compiler:$daggerVersion"
    implementation "com.google.dagger:dagger:$daggerVersion"

    //rx
    implementation 'io.reactivex.rxjava2:rxjava:2.1.10'
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.2'
    implementation 'io.reactivex.rxjava2:rxkotlin:2.2.0'
    implementation 'com.tbruyelle.rxpermissions2:rxpermissions:0.9.5@aar'

    //realm
    kapt "io.realm:realm-annotations-processor:$realmVersion"
    kapt "io.realm:realm-annotations:$realmVersion"

    //image
    implementation 'com.squareup.picasso:picasso:2.5.2'
    implementation 'com.jakewharton.picasso:picasso2-okhttp3-downloader:1.1.0'
    implementation 'com.davemorrissey.labs:subsampling-scale-image-view:3.9.0'

    //firebase
    implementation 'com.google.firebase:firebase-crash:11.8.0'
    implementation 'com.google.firebase:firebase-messaging:11.8.0'
    implementation 'com.google.firebase:firebase-perf:11.8.0'
    implementation('com.crashlytics.sdk.android:crashlytics:2.9.1@aar') {
        transitive = true
    }

    //util
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
    implementation "nz.bradcampbell:paperparcel:$paperParcelVersion"
    implementation "nz.bradcampbell:paperparcel-kotlin:$paperParcelVersion"
    kapt "nz.bradcampbell:paperparcel-compiler:$paperParcelVersion"
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:1.5.4'
    releaseImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.5.4'
    implementation 'com.jakewharton.timber:timber:4.6.1'
    implementation 'com.jakewharton.threetenabp:threetenabp:1.0.5'
    implementation('com.mikepenz:aboutlibraries:5.9.1@aar') {
        transitive = true
    }
    implementation 'com.android.support:multidex:1.0.3'
}


apply plugin: 'com.google.gms.google-services'