apply plugin: 'com.android.application'
apply plugin: 'realm-android'
apply plugin: 'com.getkeepsafe.dexcount'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.google.firebase.firebase-crash'

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()

android {
    compileSdkVersion 27
    buildToolsVersion "27.0.3"
    signingConfigs {
        if (keystorePropertiesFile.exists()) {
            keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
            release {
                keyAlias keystoreProperties['keyAlias']
                keyPassword keystoreProperties['keyPassword']
                storeFile file(keystoreProperties['storeFile'])
                storePassword keystoreProperties['storePassword']
            }
        }

    }
    defaultConfig {
        applicationId "org.sugarandrose.app"
        minSdkVersion 21
        targetSdkVersion 27
        versionCode 1
        versionName "1.0"
        vectorDrawables.useSupportLibrary = true

        multiDexEnabled true

        setProperty("archivesBaseName", "SugarAndRose-v$versionName-b$versionCode")

        buildConfigField "String", "BASE_URL", "\"https://sugarandrose.org/wp-json/wp/v2/\""
        buildConfigField "String", "WEB_PAGE", "\"https://sugarandrose.org/\""
        buildConfigField "String", "FB_NAME", "\"sugarandrose\""
        buildConfigField "String", "FB_ID", "\"1789118054658269\""
        buildConfigField "String", "INSTAGRAM_NAME", "\"sugarandrosen\""
        buildConfigField "String", "TWITTER_NAME", "\"sugarandrose\""
        buildConfigField "String", "TWITTER_ID", "\"199252121\""
        buildConfigField "String", "PINTEREST_NAME", "\"sugarandrosem\""

    }
    buildTypes {
        release {
            if (keystorePropertiesFile.exists()) {
                signingConfig signingConfigs.release
            }
            minifyEnabled true
            shrinkResources true
            proguardFiles 'proguard-rules.pro'
        }
    }
    flavorDimensions "backend"
    productFlavors {
        dev {
            signingConfig signingConfigs.debug
            applicationIdSuffix ".dev"
            resValue "string", "app_name", "sugar&rose DEV"
            dimension "backend"
        }
        prod {
            resValue "string", "app_name", "sugar&rose"
            dimension "backend"
        }
    }
    dataBinding {
        enabled = true
    }
    lintOptions {
        abortOnError false
    }
    testOptions {
        unitTests.returnDefaultValues = true
    }
    packagingOptions {
        // Remove unneeded Realm native libs (nearly no devices use these)
        exclude 'lib/mips/librealm-jni.so'
        exclude 'lib/x86_64/librealm-jni.so'
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    implementation "com.android.support:support-fragment:$supportLibVersion"
    implementation "com.android.support:support-core-ui:$supportLibVersion"
    implementation "com.android.support:appcompat-v7:$supportLibVersion"
    implementation "com.android.support:cardview-v7:$supportLibVersion"
    implementation "com.android.support:recyclerview-v7:$supportLibVersion"
    implementation "com.android.support:design:$supportLibVersion"
    implementation "com.android.support:customtabs:$supportLibVersion"

    kapt "com.android.databinding:compiler:$gradlePluginVersion"

    implementation 'com.google.code.gson:gson:2.8.2'

    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation "com.squareup.retrofit2:converter-gson:$retrofitVersion"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofitVersion"
    implementation "com.squareup.okhttp3:okhttp:$okHttpVersion"
    implementation "com.squareup.okhttp3:logging-interceptor:$okHttpVersion"

    implementation 'com.jakewharton.timber:timber:4.6.0'

    implementation "nz.bradcampbell:paperparcel:$paperParcelVersion"
    implementation "nz.bradcampbell:paperparcel-kotlin:$paperParcelVersion"
    kapt "nz.bradcampbell:paperparcel-compiler:$paperParcelVersion"

    kapt "com.google.dagger:dagger-compiler:$daggerVersion"
    implementation "com.google.dagger:dagger:$daggerVersion"

    kapt "io.realm:realm-annotations-processor:$realmVersion"
    kapt "io.realm:realm-annotations:$realmVersion"

    implementation 'io.reactivex.rxjava2:rxjava:2.1.8'
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.1'
    implementation 'io.reactivex.rxjava2:rxkotlin:2.2.0'

    implementation 'com.tbruyelle.rxpermissions2:rxpermissions:0.9.5@aar'

    debugImplementation 'com.squareup.leakcanary:leakcanary-android:1.5.4'
    releaseImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.5.4'

    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"

    testImplementation 'junit:junit:4.12'
    testImplementation "org.mockito:mockito-core:$mockitoVersion"
    testImplementation "org.powermock:powermock-module-junit4:$powerMockVersion"
    testImplementation "org.powermock:powermock-module-junit4-rule:$powerMockVersion"
    testImplementation "org.powermock:powermock-api-mockito2:$powerMockVersion"
    testImplementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
    testImplementation "com.nhaarman:mockito-kotlin-kt1.1:1.5.0"
    testImplementation "com.google.code.findbugs:jsr305:3.0.2"

    androidTestImplementation "com.android.support:support-annotations:$supportLibVersion"
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation 'com.android.support.test:rules:1.0.1'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'
    androidTestImplementation 'com.android.support.test.espresso:espresso-intents:3.0.1'
    androidTestImplementation "org.mockito:mockito-android:$mockitoVersion"
    androidTestImplementation('com.android.support.test.espresso:espresso-contrib:3.0.1') {
        exclude group: 'com.android.support', module: 'appcompat'
        exclude group: 'com.android.support', module: 'support-v4'
        exclude group: 'com.android.support', module: 'appcompat-v7'
        exclude group: 'com.android.support', module: 'design'
        exclude group: 'com.android.support', module: 'recyclerview-v7'
    }
    androidTestImplementation "com.squareup.okhttp3:okhttp:$okHttpVersion"
    androidTestImplementation 'com.jakewharton.espresso:okhttp3-idling-resource:1.0.0'
    androidTestImplementation "com.google.code.findbugs:jsr305:3.0.2"

    implementation 'com.jakewharton.threetenabp:threetenabp:1.0.5'

    implementation 'com.squareup.picasso:picasso:2.5.2'
    implementation 'com.jakewharton.picasso:picasso2-okhttp3-downloader:1.1.0'

    implementation 'com.android.support.constraint:constraint-layout:1.0.2'

    implementation 'com.google.firebase:firebase-crash:11.8.0'
    implementation 'com.google.firebase:firebase-messaging:11.8.0'

    implementation 'com.patloew.navigationviewfragmentadapters:adapters:0.3.0'

    implementation 'jp.wasabeef:recyclerview-animators:2.2.7'

    implementation 'org.jsoup:jsoup:1.11.2'

    implementation 'com.simplecityapps:recyclerview-fastscroll:1.0.17'

    implementation 'com.davemorrissey.labs:subsampling-scale-image-view:3.9.0'

    implementation('com.mikepenz:aboutlibraries:5.9.1@aar') {
        transitive = true
    }

    implementation 'com.android.support:multidex:1.0.1'
}


apply plugin: 'com.google.gms.google-services'